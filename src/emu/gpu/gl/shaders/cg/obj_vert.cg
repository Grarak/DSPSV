uniform sampler2D oamTex : TEXUNIT0;

uniform int dispCnt;

float2 out objPos : TEXCOORD0;
short3 out objProps : TEXCOORD1;

float4 out gl_Position : POSITION;

short readOam16Aligned(short addr) {
    float x = float(addr >> 2) / 255.0f;
    float4 value = tex2D(oamTex, float2(x, 1.0));
    short entry = addr & 2;
    return short(value[entry] * 255.0) | (short(value[entry + 1] * 255.0) << 8);
}

static const float2 SizeLookup[12] = {
    float2(8.0, 8.0), float2(16.0, 16.0), float2(32.0, 32.0), float2(64.0, 64.0), float2(16.0, 8.0), float2(32.0, 8.0),
    float2(32.0, 16.0), float2(64.0, 32.0), float2(8.0, 16.0), float2(8.0, 32.0), float2(16.0, 32.0), float2(32.0, 64.0),
};

void main(float2 position, float oamIndex) {
    short index = short(oamIndex);

    short attrib0 = readOam16Aligned(index * 8);
    short attrib1 = readOam16Aligned(index * 8 + 2);
    short attrib2 = readOam16Aligned(index * 8 + 4);

    int oamX = attrib1 & 0x1FF;
    int oamY = attrib0 & 0xFF;

    int shape = (attrib0 >> 12) & 0xC;
    int size = (attrib1 >> 14) & 0x3;

    float2 oamDims = SizeLookup[shape | size];
    float oamWidth = oamDims.x;
    float oamHeight = oamDims.y;

    if (oamX >= 256) {
        oamX -= 512;
    }

    if (oamY >= 192) {
        oamY -= 256;
    }

    bool is1dMap = (dispCnt & (1 << 4)) != 0;

    bool isVFlip = (attrib1 & (1 << 13)) != 0;
    bool isHFlip = (attrib1 & (1 << 12)) != 0;

    float2 pos = position;
    if (isVFlip) {
        pos.y -= 1.0;
        pos.y = abs(pos.y);
    }

    if (isHFlip) {
        pos.x -= 1.0;
        pos.x = abs(pos.x);
    }

    objPos = float2((oamWidth - 0.1) * pos.x, (oamHeight - 0.1) * pos.y);

    short objBound = 32 << (short(is1dMap) * ((dispCnt >> 20) & 0x3));
    short mapWidth = is1dMap ? short(oamWidth) : 256;
    objProps = short3(index, objBound, mapWidth);

    float x = float(oamX) + oamWidth * position.x;
    float y = float(oamY) + oamHeight * position.y;

    int priority = (attrib2 >> 10) & 3;
    gl_Position = float4(x / 256.0 * 2.0 - 1.0, 1.0 - y / 192.0 * 2.0, float(priority) / 5.0, 1.0);
}
